buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:2.0.8'
	}
}

plugins {
  id 'org.ec4j.editorconfig' version '0.0.3'
  id 'org.xtext.builder' version '2.0.8'
}

editorconfig {
  excludes = [
    '**/generated',
    '**/xtext-gen',
    '**/bin',
    '**/.*',
    'gradlew*',
    '**/out',
    '**/node_modules',
    '**/*.vsix'
  ]
}

check.dependsOn(editorconfigCheck)

subprojects {
	ext.xtextVersion = '2.23.0'
	repositories {
		mavenCentral()
	}

	apply plugin: 'java'
	dependencies {
		compile platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
	}

	apply plugin: 'org.xtext.builder'
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply plugin: 'eclipse'
    apply plugin: 'checkstyle'

	group = 'org.metafacture.flux'
	version = '1.0.0-SNAPSHOT'

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

	configurations.all {
		exclude group: 'asm'
	}

  checkstyle {
    toolVersion '8.30'
  }
}

task installServer(type: Copy) {
	dependsOn(':org.metafacture.flux.ide:installDist')
    from "org.metafacture.flux.ide/build/install/xtext-server"
    into "org.metafacture.flux.vsc/xtext-server"
}
